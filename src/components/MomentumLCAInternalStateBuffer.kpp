/*
 * MomentumLCAInternalStateBuffer.kpp
 *
 *  Created on: Dec 11, 2018
 *      Author: Pete Schultz
 */

// Creates functions updateMomentumLCAOnGPU and updateMomentumLCAOnCPU,
// that take the same argument list and use the same core code, except that
// one is run from the .cu file and the other is run from the .cpp file.
// The .cu file defines the preprocessor directive PV_RUN_ON_GPU before
// including this file; the and the .cpp file undefines it.

#ifdef PV_RUN_ON_GPU
#include "cudakernels/conversions.hcu"
namespace PVCuda {
__global__ void updateMomentumLCAOnGPU
#else
namespace PV {
inline void updateMomentumLCAOnCPU
#endif // PV_RUN_ON_GPU
      (int const nbatch,
       int const numNeurons,
       int const nx,
       int const ny,
       int const nf,
       int const lt,
       int const rt,
       int const dn,
       int const up,
       bool const selfInteract,
       float const momentumRate,
       double const *dtAdapt,
       float const tau,
       float const *accumulatedGSyn,
       float const *A,
       float *prevDrive,
       float *V) {
#ifdef PV_RUN_ON_GPU
   int index = (blockIdx.x * blockDim.x) + threadIdx.x;
   if (index < numNeurons * nbatch)
#else
#ifdef PV_USE_OPENMP_THREADS
#pragma omp parallel for schedule(static)
#endif
   for (int index = 0; index < numNeurons * nbatch; index++)
#endif // PV_RUN_ON_GPU
   {
      int b = index / numNeurons;
      int k = index % numNeurons;

      float exp_tau          = (float)std::exp(-dtAdapt[b] / (double)tau);
      float *VBatch          = V + b * numNeurons;
      float const *gSynBatch = accumulatedGSyn + b * numNeurons;
      float *prevDriveBatch  = prevDrive + b * numNeurons;
      // Activity is an extended buffer.
      float const *ABatch = A + b * (nx + rt + lt) * (ny + up + dn) * nf;

      int kex            = kIndexExtended(k, nx, ny, nf, lt, rt, dn, up);
      float currentDrive = (1.0f - exp_tau) * (gSynBatch[k] + selfInteract * ABatch[kex]);
      // Accumulate into VBatch with decay and momentum
      VBatch[k] = exp_tau * VBatch[k] + currentDrive + momentumRate * prevDriveBatch[k];
      // Update momentum buffer
      prevDriveBatch[k] = currentDrive;
   }
}

} // namespace (PV or PVCuda)
